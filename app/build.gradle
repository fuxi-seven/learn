apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion adcompileSdkVersion

    defaultConfig {
        if (isRelease) {
            applicationId adapplicationId
        }
        minSdkVersion adminSdkVersion
        targetSdkVersion adtargetSdkVersion
        versionCode adversionCode
        versionName adversionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        debug {
            //定制版本变量,编译后在BuildConfig里面会生成对应的变量,可以在java中定制使用
            //变量类型，变量名，变量内容
            buildConfigField("boolean", "isRelease", String.valueOf(isRelease))
            buildConfigField("int", "flag", "0")
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("boolean", "isRelease", String.valueOf(isRelease))
            buildConfigField("int", "flag", "1")
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    dataBinding {
        enabled = true
    }

    //配置资源环境，方便测试环境，打包不集成到正式环境
    sourceSets {
        main {
            if (isRelease) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                java {
                    //release时debug目录下文件不需要 合并到主工程
                    exclude '**/debug/**'
                }
            } else {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation androidSupportLib.appcompat
    implementation androidSupportLib.constraintlayout
    implementation androidSupportLib.lifecycleextensions
    implementation project(path: ':httpRequest')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation androidSupportLib.material
    implementation androidSupportLib.corektx
    implementation androidSupportLib.kotlin
    implementation project(path: ':lib')

    // Android 支持 Rxjava
    // 此处一定要注意使用RxJava2的版本
    implementation androidSupportLib.rxjava2
    implementation androidSupportLib.rxandroid

    // Android 支持 Retrofit
    implementation androidSupportLib.retrofit

    // 衔接 Retrofit & RxJava
    // 此处一定要注意使用RxJava2的版本
    implementation androidSupportLib.rxjava2adapter

    // 支持Gson解析
    implementation androidSupportLib.gson

    // rxbinding
    implementation androidSupportLib.rxbinding

    //Glide
    implementation androidSupportLib.glide
    annotationProcessor androidSupportLib.glidecompiler
}
repositories {
    mavenCentral()
}
